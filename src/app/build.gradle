apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "jp.kaleidot725.emomemo"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def appcompat_version = "1.0.2"
    implementation "androidx.appcompat:appcompat:${appcompat_version}"

    def core_ktx_version = "1.3.0"
    implementation "androidx.core:core-ktx:${core_ktx_version}"

    def constraint_layout_version = "1.1.3"
    implementation "androidx.constraintlayout:constraintlayout:${constraint_layout_version}"

    def nav_version = "2.2.2"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def material_version = "1.3.0-alpha01"
    implementation "com.google.android.material:material:$material_version"

    def epoxy_version = "3.9.0"
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:${epoxy_version}"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"

    def live_event_version = "1.2.0"
    implementation "com.github.hadilq.liveevent:liveevent:$live_event_version"

    def koin_version = "2.1.5"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    implementation "org.koin:koin-androidx-ext:$koin_version"

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def livedata_ktx_version = "2.2.0-alpha02"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$livedata_ktx_version"

    def permission_dispatcher_version = "4.7.0"
    implementation "org.permissionsdispatcher:permissionsdispatcher:${permission_dispatcher_version}"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:${permission_dispatcher_version}"

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    def fragment_version = "1.3.0-alpha06"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    def junit_version = "4.12"
    testImplementation "junit:junit:${junit_version}"

    def junit_ext_version = "1.1.1"
    androidTestImplementation "androidx.test.ext:junit:${junit_ext_version}"

    def espresso_core = "3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:${espresso_core}"

    def flipper_version = "0.49.0"
    def soloader_version = "0.9.0"
    debugImplementation "com.facebook.flipper:flipper:${flipper_version}"
    debugImplementation "com.facebook.soloader:soloader:${soloader_version}"
    releaseImplementation "com.facebook.flipper:flipper-noop:${flipper_version}"

}

repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    ktlint "com.pinterest:ktlint:0.33.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
