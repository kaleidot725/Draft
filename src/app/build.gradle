apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "jp.kaleidot725.emomemo"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AppCompat
    implementation "androidx.appcompat:appcompat:${versions.appcompat_version}"

    // Core Ktx
    implementation "androidx.core:core-ktx:${versions.core_ktx_version}"

    // Constraint Layout
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraint_layout_version}"

    // Navigation
    implementation "androidx.navigation:navigation-fragment:${versions.nav_version}"
    implementation "androidx.navigation:navigation-ui:${versions.nav_version}"
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.nav_version}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.nav_version}"

    // Material
    implementation "com.google.android.material:material:${versions.material_version}"

    // Epoxy
    implementation "com.airbnb.android:epoxy:${versions.epoxy_version}"
    implementation "com.airbnb.android:epoxy-databinding:${versions.epoxy_version}"
    kapt "com.airbnb.android:epoxy-processor:${versions.epoxy_version}"

    // LiveEvent
    implementation "com.github.hadilq.liveevent:liveevent:${versions.live_event_version}"

    // Koin
    implementation "org.koin:koin-androidx-scope:${versions.koin_version}"
    implementation "org.koin:koin-androidx-viewmodel:${versions.koin_version}"
    implementation "org.koin:koin-androidx-fragment:${versions.koin_version}"
    implementation "org.koin:koin-androidx-ext:${versions.koin_version}"

    // Room
    implementation "androidx.room:room-runtime:${versions.room_version}"
    implementation "androidx.room:room-ktx:${versions.room_version}"
    kapt "androidx.room:room-compiler:${versions.room_version}"

    // LiveData Ktx
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.livedata_ktx_version}"

    // Permission Dispatcher
    implementation "org.permissionsdispatcher:permissionsdispatcher:${versions.permission_dispatcher_version}"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:${versions.permission_dispatcher_version}"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${versions.lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:${versions.lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${versions.lifecycle_version}"
    kapt "androidx.lifecycle:lifecycle-compiler:${versions.lifecycle_version}"

    // Fragment Ktx
    implementation "androidx.fragment:fragment-ktx:${versions.fragment_version}"

    // Junit
    testImplementation "junit:junit:${versions.junit_version}"
    androidTestImplementation "androidx.test.ext:junit:${versions.junit_ext_version}"

    // Esporesso
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso_core}"

    // Flipper
    debugImplementation "com.facebook.flipper:flipper:${versions.flipper_version}"
    debugImplementation "com.facebook.soloader:soloader:${versions.soloader_version}"
    releaseImplementation "com.facebook.flipper:flipper-noop:${versions.flipper_version}"

}

repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    ktlint "com.pinterest:ktlint:0.33.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
